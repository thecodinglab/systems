{ root, nixpkgs, flake-utils, neovim-config, terranix, ... }:
let
  modules = {
    hermes = ./hermes;
    apollo = ./apollo;
    poseidon = ./poseidon;
    hestia = ./hestia;
  };

  args = {
    inherit root neovim-config;

    hermes.vhosts =
      (import ./apollo/vhosts.nix) //
      # TODO: (import ./hestia/vhosts.nix) //
      {
        "iot.thecodinglab.ch" = {
          locations."/" = {
            proxyPass = "http://172.16.0.65:3000";
            recommendedProxySettings = true;
          };
        };
      };
  };

  config = builtins.mapAttrs (_: import) modules;

  nixosConfigurations = builtins.mapAttrs
    (_: config: nixpkgs.lib.nixosSystem {
      system = "x86_64-linux";
      modules = [ ../proprietary-packages.nix ./base config.system ];
      specialArgs = args;
    })
    config;

  # FIXME: this requires that the reconfiguration process is always executed
  #        from the same network
  home_ipv4 = nixpkgs.lib.removeSuffix "\n" (builtins.readFile (builtins.fetchurl {
    url = "https://ipv4.icanhazip.com";
    sha256 = "0hh2p86gbq5grlx1m9p6w0yra2mwxf49mhwhxivsypc5bap5f28s";
  }));

  # FIXME: this requires that the reconfiguration process is always executed
  #        from the same machine
  home_ipv6 = nixpkgs.lib.removeSuffix "\n" (builtins.readFile (builtins.fetchurl {
    url = "https://ipv6.icanhazip.com";
    sha256 = "18pnb7imzki7icpydlln66357yk6rbkx2030jc0bs8nbj0nkk2fg";
  }));

  cloudflare = builtins.fromJSON (builtins.readFile (root + "/secrets/cloudflare.json"));
  extraLib = {
    inherit cloudflare home_ipv4 home_ipv6;

    makeCloudflareDNSRecord = (name: {
      zone_id = cloudflare.zone_id;

      type = "CNAME";
      name = name;
      value = "server.thecodinglab.ch";

      proxied = true;
      ttl = 1;

      comment = "generated by nix (https://github.com/thecodinglab/systems)";
    });
  };

  infrastructure = builtins.map
    (config: config.infrastructure { lib = extraLib; })
    (builtins.attrValues config);

  terraformConfiguration = (system: terranix.lib.terranixConfiguration {
    inherit system;

    modules = [
      # initialize incus provider
      {
        terraform.required_providers.incus = {
          source = "lxc/incus";
          version = "0.0.2";
        };

        provider.incus = { };
      }
      # initialize cloudflare provider
      {
        terraform.required_providers.cloudflare = {
          source = "cloudflare/cloudflare";
          version = "4.21.0";
        };

        provider.cloudflare = {
          api_token = cloudflare.api_token;
        };

        resource.cloudflare_record.server = {
          zone_id = cloudflare.zone_id;

          type = "A";
          name = "server";
          value = home_ipv4;

          proxied = true;
          ttl = 1;

          comment = "generated by nix (https://github.com/thecodinglab/systems)";
        };
      }
    ] ++ infrastructure;
  });

in
{
  inherit nixosConfigurations;

  packages = (system: {
    terraform-config = terraformConfiguration system;
  });

  apps = (system:
    let
      pkgs = import nixpkgs { inherit system; };

      makeOpenTofuCommandApp = (cmd: {
        type = "app";
        program = toString (pkgs.writers.writeBash cmd ''
          if [[ -h config.tf.json ]]; then rm config.tf.json; fi
          ln -s ${terraformConfiguration system} config.tf.json
          ${pkgs.opentofu}/bin/tofu init
          ${pkgs.opentofu}/bin/tofu ${cmd}
        '');
      });
    in
    {
      terraform-plan = makeOpenTofuCommandApp "plan";
      terraform-apply = makeOpenTofuCommandApp "apply";
      terraform-destroy = makeOpenTofuCommandApp "destroy";
    });
}
